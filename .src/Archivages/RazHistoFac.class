' Gambas class file

Public Sub _new()

  Me.center
  If Start.LocalSettings["/Soc" & Start.Societe & "/Materiel"] Then CheckBox1.Visible = True

End

Public Sub Button2_Click()

  CheckBox1.value = False
  Me.close

End

'**********************************************On lance le traitement**************************************
Public Sub Button1_Click()

  Dim PB As ProgBar
  Dim rResult As Result
  Dim chemin As String = "/Sauve"
  Dim Nb As Integer = 0

  If Start.son Then Music.play
  If Message.Question("Ce programme va effacer les factures archivées\nantérieures à la date saisie !\nEtes-vous d'accord pour lancer le traitement ?", "Oui", "Non") = 1 Then
    Me.mouse = mouse.Wait
    If Not Exist(User.home & chemin) Then Mkdir User.home & chemin
    Shell "mysqldump --lock-tables=0 -h " & Start.LocalSettings["/dbase/Host"] & " -u " & Utils.db.Login & " -p" & Utils.db.Password & " " & Utils.db.Name & " --add-drop-table > " & User.home & "/Sauve/" & Utils.db.Name & "EffaceF.sql" Wait
    rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabHisFac") & " where datefac < &1", Format$(DateChooser1.Value, "yyyymmdd"))
    If rResult.Available Then
      PB = New ProgBar("Suppression factures archivées", rResult.Count, Null, True, "", True)
      PB.Show()
      Repeat
        If PB.Avancement("Suppression facture " & rResult!numfac, 10) Then Break
        Utils.db.Exec("delete FROM " & Cbase.Table("TabHisLigFac") & " where num_ligfac = &1", rResult!numfac)
        Utils.db.Exec("delete FROM " & Cbase.Table("TabHisFac") & " where numfac = &1", rResult!numfac)
        Inc nb
      Until rResult.MoveNext()
      PB.Close()
    Endif
    If CheckBox1.Value = True Then
      rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabHisFacm") & " where datefac < &1", Format$(DateChooser1.Value, "yyyymmdd"))
      If rResult.Available Then
        PB = New ProgBar("Suppression factures entretien archivées", rResult.Count, Null, True, "", True)
        PB.Show()
        Repeat
          If PB.Avancement("Suppression facture " & rResult!numfac, 10) Then Break
          Utils.db.Exec("delete FROM " & Cbase.Table("TabHisLigFacm") & " where num_ligfac = &1", rResult!numfac)
          Utils.db.Exec("delete FROM " & Cbase.Table("TabHisFacm") & " where numfac = &1", rResult!numfac)
          Inc nb
        Until rResult.MoveNext()
        PB.Close()
      Endif
    Endif
    Me.mouse = mouse.Default
    Syntheses.Message("Traitement terminé ! " & nb & " factures ont été effacées.")
    Message.Info("Traitement terminé ! " & nb & " factures ont été effacées.")
  Endif

End
