' Gambas class file

Public Sub _new()

  Me.center
  If Start.LocalSettings["/Soc" & Start.Societe & "/Materiel"] Then CheckBox1.Visible = True

End

Public Sub Button2_Click()

  CheckBox1.value = False
  Me.close

End

'**********************************************On lance le traitement**************************************
Public Sub Button1_Click()

  Dim PB As ProgBar
  Dim rResult As Result
  Dim chemin As String = "/Sauve"
  Dim Nb As Integer = 0

  If Start.son Then Music.play
  If Message.Question("Ce programme va effacer les devis\nantérieurs à la date saisie !\nEtes-vous d'accord pour lancer le traitement ?", "Oui", "Non") = 1 Then
    Me.mouse = mouse.Wait
    If Not Exist(User.home & chemin) Then Mkdir User.home & chemin
    Shell "mysqldump --lock-tables=0 -h " & Start.LocalSettings["/dbase/Host"] & " -u " & Utils.db.Login & " -p" & Utils.db.Password & " " & Utils.db.Name & " --add-drop-table > " & User.home & "/Sauve/" & Utils.db.Name & "EffaceD.sql" Wait
    rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabBl") & " where datebl < &1 and type = &2", Format$(DateChooser1.Value, "yyyymmdd"), "D")
    If rResult.Available Then
      PB = New ProgBar("Suppression devis", rResult.Count, Null, True, "", True)
      PB.Show()
      Repeat
        If PB.Avancement("Suppression devis " & rResult!numbl, 10) Then Break
        Utils.db.Exec("delete FROM " & Cbase.Table("TabLigbl") & " where num_ligbl = &1", rResult!numbl)
        Utils.db.Exec("delete FROM " & Cbase.Table("TabBl") & " where numbl = &1", rResult!numbl)
        Inc nb
      Until rResult.MoveNext()
      PB.Close()
    Endif
    If CheckBox1.Value = True Then
      rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabBlM") & " where datebl < &1 and type = &2", Format$(DateChooser1.Value, "yyyymmdd"), "D")
      If rResult.Available Then
        PB = New ProgBar("Suppression devis matériel", rResult.Count, Null, True, "", True)
        PB.Show()
        Repeat
          If PB.Avancement("Suppression devis matériel " & rResult!numbl, 10) Then Break
          Utils.db.Exec("delete FROM " & Cbase.Table("TabLigblM") & " where num_ligbl = &1", rResult!numbl)
          Utils.db.Exec("delete FROM " & Cbase.Table("TabBlM") & " where numbl = &1", rResult!numbl)
          Inc nb
        Until rResult.MoveNext()
        PB.Close()
      Endif
    Endif
    Me.mouse = mouse.Default
    Syntheses.Message("Traitement terminé ! " & nb & " devis ont été effacés.")
    Message.Info("Traitement terminé ! " & nb & " devis ont été effacés.")
  Endif

End
